<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml"  width="100%" height="100%" xmlns:mentor="com.views.mentor.*" creationComplete="compInit();">
<mx:Script>
	<![CDATA[
		import mx.containers.HBox;
		import com.mappedObjects.ug.model.ResultInfo;
		import mx.controls.LinkBar;
		import mx.containers.TitleWindow;
		import mx.controls.Alert;
		import mx.rpc.events.FaultEvent;
		import mx.rpc.events.ResultEvent;
		import com.views.mentee.MenteeProfileInfo;
		import com.views.mentee.MenteeProfile;
		import mx.events.CloseEvent;
		import mx.controls.Button;
		import com.mappedObjects.ug.model.mentee.Mentee;
		import mx.controls.LinkButton;
		import mx.collections.ArrayCollection;
		import mx.rpc.remoting.RemoteObject;
		import com.interactiveObject.ServiceObject;
		import com.mappedObjects.ug.model.mentor.Mentor;
		import mx.managers.PopUpManager;
		private var menteeProfileInfo:MenteeProfileInfo;
		private var tileWindow:TitleWindow;
		private var serviceObject:ServiceObject;
		private var menteeRemoteObj:RemoteObject;
		private var mentorRmtObject:RemoteObject;
		private var menteeList:ArrayCollection;
		private var menteeName:LinkButton;
		private var accept_Btn:Button;
		private var reject_Btn:Button;
		private var menteeDet:HBox;
		private var mentorObject:Mentor;
		private var resultInfoObj:ResultInfo;
		private var menteeObj:Mentee;
		private function compInit():void
		{
			serviceObject = new ServiceObject;
			menteeRemoteObj = serviceObject.getRemoteObjectInstance("menteeManager");
			mentorRmtObject = serviceObject.getRemoteObjectInstance("mentorManager");
			mentorRmtObject.addMentee.addEventListener(ResultEvent.RESULT,onResultAddMentee,false,0,true);
			mentorRmtObject.addMentee.addEventListener(FaultEvent.FAULT,onFaultAddMentee,false,0,true);
			mentorRmtObject.detachMentee.addEventListener(ResultEvent.RESULT,onResultDetachMentee,false,0,true);
			mentorRmtObject.detachMentee.addEventListener(FaultEvent.FAULT,onFaultDetachMentee,false,0,true);
			
			menteeRemoteObj.getAvailableMentees.addEventListener(ResultEvent.RESULT,onResultGetAvaiableMentee,false,0,true);
			menteeRemoteObj.getAvailableMentees.addEventListener(FaultEvent.FAULT,onFaultGetAvaiableMentee,false,0,true);
			menteeRemoteObj.getAvailableMentees();
		}
		
		private function onResultAddMentee(event:ResultEvent):void
		{
			resultInfoObj = event.result as ResultInfo;
			if(resultInfoObj.success) Alert.show(resultInfoObj.responseDetail,"Info");
			else Alert.show(resultInfoObj.responseDetail,"Info");
		}
		
		private function onFaultAddMentee(event:FaultEvent):void
		{
			Alert.show(event.fault.message,"Error");
		}
		
		private function onResultDetachMentee(event:ResultEvent):void
		{
			resultInfoObj = event.result as ResultInfo;
			if(resultInfoObj.success) Alert.show(resultInfoObj.responseDetail,"Info");
			else Alert.show(resultInfoObj.responseDetail,"Info");
		}
		
		private function onFaultDetachMentee(event:FaultEvent):void
		{
			Alert.show(event.fault.message,"Error");
		}
		
		
		public function setMentorObject(mentorObj:Mentor):void
		{
			mentorObject = mentorObj;
			mentorProfile.setMentorObject(mentorObj);
		}
		
		private function onResultGetAvaiableMentee(event:ResultEvent):void
		{
			menteeList = event.result as ArrayCollection;
			mentorProfile.menteeCount = menteeList.length;
			for each(var mentee:Mentee in menteeList){
				menteeName = new LinkButton;
				menteeName.label = mentee.firstName;
				menteeName.addEventListener(MouseEvent.CLICK,openMenteeProfile,false,0,true);
				accept_Btn = new Button;
				accept_Btn.label = "ACCEPT";
				accept_Btn.addEventListener(MouseEvent.CLICK,onAccept,false,0,true);
				reject_Btn = new Button;
				reject_Btn.label = "REJECT";
				reject_Btn.addEventListener(MouseEvent.CLICK,onReject,false,0,true);
				menteeDet = new HBox;
				menteeDet.addChild(menteeName);
				menteeDet.addChild(accept_Btn);
				menteeDet.addChild(reject_Btn);
				menteeDisplayList.addChild(menteeDet);
			}
		}
		
		private function onFaultGetAvaiableMentee(event:FaultEvent):void
		{
			Alert.show(event.fault.message,"Error");
		}
		
		private function onAccept(event:MouseEvent):void
		{
			menteeObj = fetchMenteeObject(event);
			mentorRmtObject.addMentee(mentorObject.email,menteeObj);
		}
		
		private function fetchMenteeObject(event:MouseEvent):Mentee
		{
			var menteeInfo:HBox = event.currentTarget.parent as HBox;
			var menteeLinkBtn:LinkButton = menteeInfo.getChildAt(0) as LinkButton;
			var menteeObj:Mentee = returnMenteeObject(menteeLinkBtn.label);
			return menteeObj;
		}
		
		private function onReject(event:MouseEvent):void
		{
			menteeObj = fetchMenteeObject(event);
			mentorRmtObject.detachMentee(mentorObject.email,menteeObj);
		}
		
		private function openMenteeProfile(event:MouseEvent):void
		{
			menteeProfileInfo = new MenteeProfileInfo;
			var linkBtn:LinkButton = event.target as LinkButton;
			menteeProfileInfo.menteeObject = returnMenteeObject(linkBtn.label);
			tileWindow = new TitleWindow;
			tileWindow.title = "Mentee Profile Info";
			tileWindow.percentHeight = 100;
			tileWindow.percentWidth = 100;
			tileWindow.showCloseButton = true;
			tileWindow.addChild(menteeProfileInfo);
			PopUpManager.addPopUp(tileWindow,this,true);
//			PopUpManager.createPopUp(tileWindow,this);
			tileWindow.addEventListener(CloseEvent.CLOSE,onClose,false,0,true);
		}
		
		private function onClose(event:CloseEvent):void
		{
			PopUpManager.removePopUp(tileWindow);
			tileWindow.removeEventListener(CloseEvent.CLOSE,onClose,false);
			menteeProfileInfo = null;
			tileWindow = null;
		}
		
		private function returnMenteeObject(menteeName:String):Mentee
		{
			for each(var mentee:Mentee in menteeList){
				if(mentee.firstName == menteeName)
					return mentee;
			}
			return null;
		}

	]]>
</mx:Script>
<mx:HDividedBox width="100%" height="100%">
<mentor:MentorTestimonialLinks width="10%" height="100%"/>
	<mx:VBox width="100%" height="100%" borderStyle="solid">
		<mx:Text width="100%" text="Congratulations, you have successfully signed up as a Mentor at UnivGiggle. 
		Thanks for taking the first step towards making a difference in someoneâ€™s life by sharing your life 
		experience with someone in need of guidance and direction. We can assure you that you will have a 
		very hearty experience through this program. You can see your life changing when you try bringing a 
		difference in the life of someone.This is also a great way to give back to your society, 
		alma mater, country and mankind as a whole." fontWeight="bold"/>
		<mx:Spacer height="20%"/>
		<mx:HBox width="100%">
			<mentor:MentorProfileInfo id="mentorProfile" width="50%"/>
			<mx:VBox width="50%" horizontalAlign="left">
				<mx:Text text="Outstanding Mentee Requests.Take Action"/>
				<mx:VBox id="menteeDisplayList"/>
				<!--<mx:HBox id="menteeDet">
					<mx:Text text="Mentee1" />
					<mx:Button label="ACCEPT"/>
					<mx:Button label="REJECT"/>
				</mx:HBox>-->
			</mx:VBox>	
		</mx:HBox>
	</mx:VBox>
</mx:HDividedBox>
</mx:VBox>
